96b4c42b9477a8037b3bd64ab5593485
Require Import Coq.Lists.List.
Require Import Events.
Import ListNotations.
Module Signature.
Definition t := list Type.
End Signature.
Module Memory.
Inductive t : Signature.t -> Type :=
  | Nil : t []
  | Cons : forall (A : Type) (sig : Signature.t), A -> t sig -> t (A :: sig).
Definition head {A : Type} {sig : Signature.t} (mem : t (A :: sig)) : A :=
    match mem with
    | Cons _ _ x _ => x
    end.
Definition tail {A : Type} {sig : Signature.t} (mem : t (A :: sig)) : t sig :=
    match mem with
    | Cons _ _ _ mem => mem
    end.
End Memory.
Module Ref.
Class C (A : Type) (sig : Signature.t) : Type := New {
    read : Memory.t sig -> A;
    write : Memory.t sig -> A -> Memory.t sig }.
Instance cons_left (A : Type) (sig : Signature.t) : C A (A :: sig) := {
    read mem := Memory.head mem;
    write mem x := Memory.Cons _ _ x (Memory.tail mem) }.
Instance cons_right (A B : Type) (sig : Signature.t) (I : C A sig)
    : C A (B :: sig) := {
    read mem := read (Memory.tail mem);
    write mem x :=
      Memory.Cons _ _ (Memory.head mem) (write (Memory.tail mem) x) }.
End Ref.
Module C.
Inductive t (sig : Signature.t) : Type -> Type :=
  | Ret : forall (A : Type), A -> t sig A
  | Bind : forall (A B : Type), t sig A -> (A -> t sig B) -> t sig B
  | Read : forall (A : Type), `{Ref.C A sig} -> t sig A
    
  | Write : forall (A : Type), `{Ref.C A sig} -> A -> t sig unit
    
  | Send : forall (A : Type) (command : Command.t), Command.request command ->
    A -> (A -> Command.answer command -> t sig (option A)) -> t sig unit
    
  | Exit : forall (A : Type), t sig A.
Arguments Ret {sig A} _.
Arguments Bind {sig A B} _ _.
Arguments Read {sig A} _.
Arguments Write {sig A} _.
Arguments Send {sig A} _ _ _ _.
Arguments Exit {sig A}.
Module Notations.
Notation "'let!' X ':=' A 'in' B" := (Bind A (fun X => B))
      (at level 200, X ident, A at level 100, B at level 200).
Notation "'let!' X ':' T ':=' A 'in' B" := (Bind (A := T) A (fun X => B))
      (at level 200, X ident, A at level 100, T at level 200, B at level 200).
Notation "'do!' A 'in' B" := (Bind A (fun _ => B))
      (at level 200, B at level 200).
End Notations.
End C.
